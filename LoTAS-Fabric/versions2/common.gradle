sourceCompatibility = 1.8
targetCompatibility = 1.8

version = "2.0.0-SNAPSHOT"
group = "de.pfannekuchen.lotas"
archivesBaseName = "lotas"

repositories {
	maven { url 'https://mgnet.work/repo' }
	maven { url 'https://jitpack.io' }
}

configurations {
	include
	implementation.extendsFrom(include)
}

dependencies {
    implementation 'org.projectlombok:lombok:1.16.4'
	include 'work.mgnet:identifier:1.1'
    include 'net.bramp.ffmpeg:ffmpeg:2.8.0-21.0'
    include 'org.slf4j:slf4j-api:1.7.25'
    include 'com.google.code.findbugs:annotations:3.0.1u2'
    include 'com.google.errorprone:error_prone_annotations:2.0.19'
    include 'org.modelmapper:modelmapper:0.7.7'
    modImplementation "net.fabricmc:fabric-loader:0.11.3"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

build.dependsOn processResources
defaultTasks 'build'

minecraft {
	accessWidener '../../src/main/resources/lotas.accesswidener'
}

jar {
	from {
    	configurations.include.collect { it.isDirectory() ? it : zipTree(it) }
  	}
  	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

if (project.name != 'fabric') {
    apply from: '../preprocessor.gradle'

    def preprocessedSrc = 'build/preprocessed/src'
    def preprocessedRes = 'build/preprocessed/res'
    def originalSrc = '../../src/main/java'
    def originalRes = '../../src/main/resources'
    def (major, minor, patch) = project.name.tokenize('.')
    def mcVersion = "${major}${minor.padLeft(2, '0')}${(patch ?: '').padLeft(2, '0')}"
    def vars = [MC: mcVersion as int]

    sourceSets {
        main.java.srcDir preprocessedSrc
        main.resources.srcDir preprocessedRes
    }

    task preprocessJava {
        inputs.dir(originalSrc)
        outputs.dir(preprocessedSrc)
        doLast {
            project.convertTree(vars, originalSrc, preprocessedSrc)
        }
    }

    task preprocessResources {
        inputs.dir(originalRes)
        outputs.dir(preprocessedRes)
        doLast {
            project.convertTree(vars, originalRes, preprocessedRes)
        }
    }

    compileJava.dependsOn preprocessJava
    processResources.dependsOn preprocessResources
}

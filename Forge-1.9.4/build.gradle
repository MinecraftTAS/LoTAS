buildscript {
    repositories {
    	maven { url = "https://sizableshrimp.me/maven" }
        jcenter()
        maven { url = "https://files.minecraftforge.net/maven" }
        maven { url = "https://repo.spongepowered.org/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
    }
}
plugins {
    id 'com.github.johnrengelman.shadow' version '2.0.4'
    id 'java'
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'

version = "1.0.0"
group = "de.pfannekuchen.lotas"
archivesBaseName = "lotas"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}
repositories {
    mavenCentral()
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven'
    }
}

minecraft {
    version = "1.9.4-12.17.0.2051"
    runDir = "run"
    
    mappings = "snapshot_20160518"
    //makeObfSourceJar = false
    replace '${version}':project.version,'${mcversion}':project.minecraft.version
}

dependencies {
    implementation('org.spongepowered:mixin:0.7.10-SNAPSHOT') {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
        exclude module: 'gson'
        exclude module: 'commons-io'
        exclude module: 'log4j-core'
    }
    implementation files ('libs/rLog.jar')
    implementation files ('libs/identifier_obfc.jar')    
    compile files ('libs/ffmpeg.jar')    
    implementation 'org.msgpack:msgpack-core:0.8.16'
    implementation 'org.msgpack:jackson-dataformat-msgpack:0.8.16'
}
jar {
    manifest {
        attributes 'FMLAT': 'lotas_at.cfg',
                   'FMLCorePluginContainsFMLMod':'true',
                   'MixinConfigs':'mixin.lotas.json',
                   'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
                   'ForceLoadAsMod': 'true'
    }
}
processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
shadowJar {
    mainSpec.sourcePaths.clear()

    dependsOn reobfJar

    configurations = [project.configurations.compile]
    relocate 'org.msgpack', 'de.pfannekuchen.lotas.repack.org.msgpack'
    relocate 'com.fasterxml', 'de.pfannekuchen.lotas.repack.com.fasterxml'
    classifier "shadow"
    exclude 'dummyThing'

    afterEvaluate {
        from zipTree(reobfJar.jar)
    }
}
mixin {
    defaultObfuscationEnv searge
    add sourceSets.main, "mixin.lotas.refmap.json"
}


// :)

project.getAllTasks(true).each { key, val -> 
	val.each {
		it.group = null
	}	
}

tasks.getByName("shadowJar").group = "forge"
tasks.getByName("setupDecompWorkspace").group = "forge"
tasks.getByName("runClient").group = "forge"
tasks.getByName("runServer").group = "forge"
tasks.getByName("javadoc").group = "forge"
tasks.getByName("eclipse").group = "forge"
